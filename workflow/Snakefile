configfile: "config/config.yml"

##################################################################
##                         Author Info                          ##
##################################################################

# Author: Kevin A. Boyd
# Email: kevinboyd76@gmail.com
# GitHub: https://github.com/kevinboyd76
# Date Created: June 23, 2025
# Last Modified: June 23, 2025
# Version: 1.0.0

##################################################################
##                    Define input functions                    ##
##################################################################

import os
import pandas as pd

# load samples table and set samples naming
samples_df = pd.read_csv("config/samples.csv").set_index("sample", drop=False)
SAMPLES = samples_df.index.tolist()

# dictionary based input function
def fq_dict_from_sample(wildcards):
    row = samples_df.loc[wildcards.sample]
    return {
        "r1": row["fastq1"],
        "r2": row["fastq2"]
    }

##################################################################
##                          Rule All                            ##
##################################################################

rule all:
    input:
        # Raw QC
        expand("results/qc/raw/fastqc/{sample}_R1_fastqc.html", sample=SAMPLES),
        expand("results/qc/raw/fastqc/{sample}_R2_fastqc.html", sample=SAMPLES),
        "results/qc/raw/multiqc/multiqc_report.html",
        
        # Trim R1 and R2 files
        expand("results/trimmed/{sample}_R1_trimmed.fastq.gz", sample=SAMPLES),
        expand("results/trimmed/{sample}_R2_trimmed.fastq.gz", sample=SAMPLES),
        expand("results/trimmed/{sample}_fastp.html", sample=SAMPLES),
        expand("results/trimmed/{sample}_fastp.json", sample=SAMPLES),
        
        # Trimmed QC
        expand("results/qc/trimmed/fastqc/{sample}_R1_trimmed_fastqc.html", sample=SAMPLES),
        expand("results/qc/trimmed/fastqc/{sample}_R2_trimmed_fastqc.html", sample=SAMPLES),
        "results/qc/trimmed/multiqc/multiqc_report.html",
        
        # Alignment and Index
        expand("results/aligned/{sample}.sorted.bam", sample=SAMPLES),
        expand("results/aligned/{sample}.sorted.bam.bai", sample=SAMPLES),

        # Quantification and Merging Counts
        expand("results/counts/{sample}.counts.txt", sample=SAMPLES),
        "results/counts/merged_counts.csv"

##################################################################
##                     FastQC on Raw Fastqs                     ##
##################################################################

rule fastqc_raw:
    input:
        unpack(fq_dict_from_sample)
    output:
        html1="results/qc/raw/fastqc/{sample}_R1_fastqc.html",
        html2="results/qc/raw/fastqc/{sample}_R2_fastqc.html",
        zip1 ="results/qc/raw/fastqc/{sample}_R1_fastqc.zip",
        zip2 ="results/qc/raw/fastqc/{sample}_R2_fastqc.zip"
    envmodules:
        config["fastqc"]
    log:
        "results/logs/fastqc/{sample}_raw.log"
    shell:
        """
        fastqc -o results/qc/raw/fastqc {input.r1} {input.r2} 2> {log}

        dir="results/qc/raw/fastqc"
        baseR1=$(basename {input.r1} .fastq.gz)
        mv $dir/${{baseR1}}_fastqc.html {output.html1} || true
        mv $dir/${{baseR1}}_fastqc.zip {output.zip1} || true

        baseR2=$(basename {input.r2} .fastq.gz)
        mv $dir/${{baseR2}}_fastqc.html {output.html2} || true
        mv $dir/${{baseR2}}_fastqc.zip {output.zip2} || true
        """

##################################################################
##                    MultiQC on Raw Fastqs                    ##
##################################################################

rule multiqc_raw:
    input:
        expand("results/qc/raw/fastqc/{sample}_R1_fastqc.zip", sample=SAMPLES),
        expand("results/qc/raw/fastqc/{sample}_R2_fastqc.zip", sample=SAMPLES)
    output:
        "results/qc/raw/multiqc/multiqc_report.html"
    params:
        outdir="results/qc/raw/multiqc"
    envmodules:
        config["multiqc"]
    log:
        "results/logs/multiqc/multiqc_raw.log"
    shell:
        """
        multiqc results/qc/raw/fastqc -o {params.outdir} 2> {log}
        """


##################################################################
##                      Adapter Trimming                        ##
##################################################################

rule trim_adapters:
    input:
        unpack(fq_dict_from_sample)
    output:
        r1_trim="results/trimmed/{sample}_R1_trimmed.fastq.gz",
        r2_trim="results/trimmed/{sample}_R2_trimmed.fastq.gz",
        html="results/trimmed/{sample}_fastp.html",
        json="results/trimmed/{sample}_fastp.json"
    envmodules:
        config["fastp"]
    log:
        "results/logs/trim/{sample}.log"
    shell:
        """
        mkdir -p results/trimmed
        fastp \
            --in1 {input.r1} \
            --in2 {input.r2} \
            --out1 {output.r1_trim} \
            --out2 {output.r2_trim} \
            --html {output.html} \
            --json {output.json} \
            --thread 4 \
            2> {log}
        """


##################################################################
##                   FastQC on Trimmed Fastqs                   ##
##################################################################

rule fastqc_trimmed:
    input:
        r1_trim="results/trimmed/{sample}_R1_trimmed.fastq.gz",
        r2_trim="results/trimmed/{sample}_R2_trimmed.fastq.gz"
    output:
        html3="results/qc/trimmed/fastqc/{sample}_R1_trimmed_fastqc.html",
        html4="results/qc/trimmed/fastqc/{sample}_R2_trimmed_fastqc.html",
        zip3 ="results/qc/trimmed/fastqc/{sample}_R1_trimmed_fastqc.zip",
        zip4 ="results/qc/trimmed/fastqc/{sample}_R2_trimmed_fastqc.zip"
    envmodules:
        config["fastqc"]
    log:
        "results/logs/fastqc/{sample}_trimmed.log"
    shell:
        """
        fastqc -o results/qc/trimmed/fastqc {input.r1_trim} {input.r2_trim} 2> {log}
        """

##################################################################
##                  MultiQC on Trimmed Fastqs                  ##
##################################################################

rule multiqc_trimmed:
    input:
        expand("results/qc/trimmed/fastqc/{sample}_R1_trimmed_fastqc.zip", sample=SAMPLES),
        expand("results/qc/trimmed/fastqc/{sample}_R2_trimmed_fastqc.zip", sample=SAMPLES)
    output:
        "results/qc/trimmed/multiqc/multiqc_report.html"
    params:
        outdir="results/qc/trimmed/multiqc"
    envmodules:
        config["multiqc"]
    log:
        "results/logs/multiqc/multiqc_trimmed.log"
    shell:
        """
        multiqc results/qc/trimmed/fastqc -o {params.outdir} 2> {log}
        """

##################################################################
##                       HISAT2 Alignment                       ##
##################################################################

rule align_hisat2:
    input:
        r1_trim="results/trimmed/{sample}_R1_trimmed.fastq.gz",
        r2_trim="results/trimmed/{sample}_R2_trimmed.fastq.gz"
    output:
        bam="results/aligned/{sample}.sorted.bam",
        bai="results/aligned/{sample}.sorted.bam.bai"
    params:
        index=config["Hisat2_Index"]
    envmodules:
        config["hisat2"],
        config["samtools"]
    log:
        "results/logs/alignment/{sample}.log"
    shell:
        """
        hisat2 -p 8 \
            -x {params.index} \
            -1 {input.r1_trim} \
            -2 {input.r2_trim} \
            -S results/aligned/{wildcards.sample}.sam 2> {log}

        samtools sort -@ 8 -o {output.bam} results/aligned/{wildcards.sample}.sam
        samtools index {output.bam}
        """


##################################################################
##                       Feature Counts                         ##
##################################################################

rule featurecounts:
    input:
        bam="results/aligned/{sample}.sorted.bam"
    output:
        counts="results/counts/{sample}.counts.txt"
    params:
        gtf=config["Annotation_GTF"]
    envmodules:
        config["subread"]
    log:
        "results/logs/counts/{sample}.log"
    shell:
        """
        featureCounts -T 4 -p -t exon -g gene_id -a {params.gtf} -o {output.counts} {input.bam} 2> {log}
        """


##################################################################
##                        Merge Counts                          ##
##################################################################

rule merge_counts:
    input:
        expand("results/counts/{sample}.counts.txt", sample=SAMPLES)
    output:
        "results/counts/merged_counts.csv"
    run:
        dfs = []
        for f in input:
            sample = f.split("/")[-1].replace(".counts.txt", "")
            df = pd.read_csv(f, sep="\t", comment="#", usecols=[0, -1])  # Get geneid and last column (counts)
            df.columns = ["Geneid", sample]
            dfs.append(df)

        # Merge on Geneid
        merged = dfs[0]
        for df in dfs[1:]:
            merged = merged.merge(df, on="Geneid")

        merged.to_csv(output[0], sep=",", index=False)
